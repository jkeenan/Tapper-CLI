#! /usr/bin/env perl

use strict;
use warnings;

use Artemis::Config;
use Artemis::Schema::TestrunDB;

my $env = $ARGV[0] || '';

sub usage {
        print "$0 [ test | development | live ]\n";
        print "Deploys a Testrun-DB with connection info from Artemis-config.\n";
        return -1;
}

sub no_live {
        print "We currently don't do live deployment.\n";
        return -1;
}

sub insert_initial_values {
        my ($schema) = @_;

        # ---------- Topic ----------

        # official topics
        my %topic_description = %Artemis::Schema::TestrunDB::Result::Topic::topic_description;

        foreach my $topic_name(keys %topic_description) {
                my $topic = $schema->resultset('Topic')->new
                    ({ name        => $topic_name,
                       description => $topic_description{$topic_name},
                     });
                $topic->insert;
        }

        # ---------- Preconditiontype ----------

        # official preconditiontypes
        my %preconditiontype_description = %Artemis::Schema::TestrunDB::Result::Preconditiontype::preconditiontype_description;

        foreach my $type_name(keys %preconditiontype_description) {
                my $preconditiontype = $schema->resultset('Preconditiontype')->new({ name        => $type_name,
                                                                                     description => $preconditiontype_description{$type_name},
                                                                                   });
                $preconditiontype->insert;
        }

        # ---------- User ----------

        my @users = (
                     [ 'Boris Petkov',            'bpetkov',  '' ],
                     [ 'Conny Seidel',            'cseidel',  '' ],
                     [ 'Frank Arnold',            'farnold',  '' ],
                     [ 'Frank Becker',            'fbecker',  '' ],
                     [ 'Jan Krocker',             'jkrocke2', '' ],
                     [ 'Joerg Roemer',            'jroemer',  '' ],
                     [ 'Maik Hentsche',           'mhentsc3', '' ],
                     [ 'Steffen Schwigon',        'sschwigo', '' ],
                     [ 'Steffen Schwigon@bascha', 'ss5',      '' ],
                    );

        foreach (@users) {
                my $user = $schema->resultset('User')->new
                    ({
                      name     => $_->[0],
                      login    => $_->[1],
                      password => $_->[2],
                     });
                $user->insert;
        }
}

sub deploy_testrundb
{
        my $dsn  = Artemis::Config->subconfig->{database}{TestrunDB}{dsn};
        my $user = Artemis::Config->subconfig->{database}{TestrunDB}{username};
        my $pw   = Artemis::Config->subconfig->{database}{TestrunDB}{password};

        # ----- really? -----
        print "dsn = $dsn\n";
        print "Really delete all existing content and initialize from scratch (y/N)? ";
        read STDIN, my $answer, 1;
        do { print "Quit.\n"; return } unless lc $answer eq 'y';

        # ----- delete sqlite file -----
        if ($dsn =~ /dbi:SQLite:dbname/) {
                my ($tmpfname) = $dsn =~ m,dbi:SQLite:dbname=([\w./]+),i;
                unlink $tmpfname;
        }

        my $schema = Artemis::Schema::TestrunDB->connect ($dsn, $user, $pw);
        $schema->deploy({ add_drop_table => 1 });
        insert_initial_values($schema);
}

exit usage()   unless $env =~ /^test|development|live$/;
exit no_live() if     $env eq 'live';

Artemis::Config::_switch_context($env);
deploy_testrundb();

# Local Variables:
# buffer-file-coding-system:utf-8
# End:
