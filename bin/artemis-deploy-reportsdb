#! /usr/bin/env perl

use strict;
use warnings;

use 5.010;

use Artemis::Config;
use Artemis::Schema::ReportsDB;

my $env = $ARGV[0] || '';

sub usage {
        print "$0 [ test | development | live ]\n";
        print "Deploys a Reports-DB with connection info from Artemis-config.\n";
        return -1;
}

sub no_live {
        print "We currently don't do live deployment.\n";
        return -1;
}

sub insert_initial_values {
        my ($schema) = @_;

        # ---------- User ----------

        my @users = (
                     [ 'Boris Petkov',            'bpetkov',  '' ],
                     [ 'Conny Seidel',            'cseidel',  '' ],
                     [ 'Frank Arnold',            'farnold',  '' ],
                     [ 'Frank Becker',            'fbecker',  '' ],
                     [ 'Jan Krocker',             'jkrocke2', '' ],
                     [ 'Joerg Roemer',            'jroemer',  '' ],
                     [ 'Maik Hentsche',           'mhentsc3', '' ],
                     [ 'Steffen Schwigon',        'sschwigo', '' ],
                     [ 'Steffen Schwigon@bascha', 'ss5',      '' ],
                    );

        foreach (@users) {
                say STDERR "Add ", join(", ", @$_);
                my $user = $schema->resultset('User')->new
                    ({
                      name     => $_->[0],
                      login    => $_->[1],
                      password => $_->[2],
                     });
                $user->insert;
                #say STDERR "Got ID ", $user->id;
        }
}

sub deploy_reportsdb
{
        my $dsn  = Artemis::Config->subconfig->{database}{ReportsDB}{dsn};
        my $user = Artemis::Config->subconfig->{database}{ReportsDB}{username};
        my $pw   = Artemis::Config->subconfig->{database}{ReportsDB}{password};

        # ----- really? -----
        print "dsn = $dsn\n";
        print "Really delete all existing content and initialize from scratch (y/N)? ";
        read STDIN, my $answer, 1;
        do { print "Quit.\n"; return } unless lc $answer eq 'y';

        # ----- delete sqlite file -----
        if ($dsn =~ /dbi:SQLite:dbname/) {
                my ($tmpfname) = $dsn =~ m,dbi:SQLite:dbname=([\w./]+),i;
                unlink $tmpfname;
        }

        my $schema = Artemis::Schema::ReportsDB->connect ($dsn, $user, $pw);
        $schema->deploy({ add_drop_table => 1 });
        insert_initial_values($schema);
}

exit usage()   unless $env =~ /^test|development|live$/;
exit no_live() if     $env eq 'live';

Artemis::Config::_switch_context($env);
deploy_reportsdb();

# Local Variables:
# buffer-file-coding-system:utf-8
# End:
